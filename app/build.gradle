apply plugin: 'com.android.application'

//noinspection GroovyMissingReturnStatement
android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.hedgehogproductions.therapyguide"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 6
        versionName "1.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            // Run code coverage reports by default on debug builds.
            testCoverageEnabled = true
        }
    }

    flavorDimensions "test"
    productFlavors {
        mock {
            dimension "test"
            applicationIdSuffix = ".mock"
        }
        prod {
            dimension "test"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if(variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true);
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.android.billingclient:billing:1.0'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.google.guava:guava:23.3-android'
    implementation 'com.google.code.findbugs:jsr305:3.0.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.7.19'

    // Android Testing Support Library's runner and rules
    androidTestImplementation('com.android.support.test:runner:0.5', {
        exclude group: 'com.android.support'
    })
    androidTestImplementation('com.android.support.test:rules:0.5', {
        exclude group: 'com.android.support'
    })

    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.1'
    // Espresso UI Testing dependencies.
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support'
    })
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:3.0.2', {
        exclude group: 'com.android.support'
    })
    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'
}
